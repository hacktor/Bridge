#!/usr/bin/perl -w
#
# Signal Daemon for Hermod Gateway Bot.
#
# Keeping a tail on the signal-cli dbus daemon for new messages from signal.
# These are send to other chats
#
# 2021, Ruben de Groot

use strict;
use threads;
use JSON;
use TOML;
use Any::URI::Escape;
use Capture::Tiny 'tee';
use Hermod;
use Encode qw(decode_utf8);

open my $fh, '<', "/etc/hermod.toml" or die "error opening configuration $!";
my ($cfg, $e) = from_toml do { local $/; <$fh> };
unless ($cfg) {
    die "Error parsing toml: $e";
}

unless (defined $cfg->{signal}->{phone} and defined $cfg->{signal}->{cli} and
        defined $cfg->{signal}->{gid}) {
    print "Please define signal->phone, signal->cli and signal->gidi\n";
    print "Press <Ctrl>-C to continue\n";
    sleep; exit;
}

my $sig = $cfg->{signal};
$ENV{JAVA_HOME} = $sig->{JAVA_HOME} if defined $sig->{JAVA_HOME};
open my $dbg, ">>", $sig->{debug} if defined $sig->{debug};
my $tel = $cfg->{telegram} if defined $cfg->{telegram};
my $irc = $cfg->{irc} if defined $cfg->{irc};
my $mat = $cfg->{matrix} if defined $cfg->{matrix};
my $mm = $cfg->{mattermost} if defined $cfg->{mattermost};
my $dis = $cfg->{discord} if defined $cfg->{discord};

# start a thread to keep a tail on the signal->infile
my $thr = threads->create(\&signalinfilereader,$sig,$dbg)
	unless defined $sig->{transport} or $sig->{transport} =~ /file/i;

# tailing signal dbus
#open(my $dbus, "-|", "$sig->{cli}", "-u", "$sig->{phone}", "--output=json", "daemon", "--dbus");

for (;;) {

    my ($dbus, $dbuserr, $ret) =  tee {
        system("$sig->{cli}", "-u", "$sig->{phone}", "--output=json", "daemon", "--dbus");
    };
    # now poll the signal group for new messages
    my $json = JSON->new->allow_nonref;
    my $err = <$dbuserr>;
    print "STDERR: $err\n";
    while (my $line = <$dbus>) {

        print $line;
        print $dbg $line if defined $dbg;

        my $sigmsg = $json->decode($line);
        next unless defined $sigmsg->{envelope}->{dataMessage};
        $sigmsg = $sigmsg->{envelope};
        my $datamsg = $sigmsg->{dataMessage};

        my $sender = (defined $sigmsg->{sourceName}) ? $sigmsg->{sourceName} : $sig->{anon};
        my $group = (defined $datamsg->{groupInfo}->{groupId}) ? $datamsg->{groupInfo}->{groupId} : "";
        my $attach = (defined $datamsg->{attachments}) ? $datamsg->{attachments} : undef;

        my $msg = (defined $datamsg->{message}) ? $datamsg->{message} : "";
        my $text = '';
        my $sav = $msg;
        eval { $text .= decode_utf8($msg, Encode::FB_QUIET) while $msg; };
        $text = $sav if $@; # try undecoded string as last resort

        # is the message a quote
        my $quote = "";
        if (defined $datamsg->{quote}{text}) {
            my $qtext = $datamsg->{quote}{text}; chomp $qtext;
            $qtext = (length($qtext) > 100) ? "(reply to ". substr($qtext,0,100) ."...) " : "(reply to: $qtext) ";
            $sav = $qtext;
            eval { $quote .= decode_utf8($qtext, Encode::FB_QUIET) while $qtext; };
            $quote = $sav if $@; # try undecoded string as last resort
        }

        # only relay group messages with contents
        next unless $group eq $sig->{gid};
        next unless $text or $attach;

        # relay to all chats
        my $pre = "[sig] $sender: ";
        if ($text =~ /\S/) {

            Hermod::relay2tel($tel,"$pre$quote$text\n",$dbg);
            Hermod::relay2irc("$quote$text\n",$irc,$pre,$dbg);
            Hermod::relay2mm("$pre$quote$text\n",$mm,$dbg);
            Hermod::relay2mtx("$pre$quote$text\n",$mat,$dbg);
            Hermod::relay2dis("$pre$quote$text\n",$dis,$dbg);
        }

        # relay optional attachments
        foreach my $att (@$attach) {
            (my $ext = $att->{contentType}) =~ s#.*/##;
            rename "$sig->{attachments}/$att->{id}", "$sig->{attachments}/$att->{id}.$ext";
            my $type = ($att->{contentType} =~ /image/) ? 'photo' : 'document';

            my $filemsg = "FILE!$sig->{url}/$att->{id}.$ext!$att->{contentType}!$sig->{attachments}/$att->{id}.$ext [sig] $type by $sender\n";
            my $msg = "[sig] **$sender sends a $type: $sig->{url}/$att->{id}.$ext\n";

            (defined $mm->{bearer}) ?
                Hermod::relayFile2mm($filemsg,$mm,$dbg) : 
                Hermod::relay2mm($msg,$mm,$dbg);
            Hermod::relayFile2tel($filemsg,$tel,$type,$dbg);
            Hermod::relay2mtx($msg,$mat,$dbg);
            Hermod::relay2dis($msg,$dis,$dbg);
            Hermod::relay2irc($msg,$irc,"",$dbg);
        }
    }
}

sub signalinfilereader {

    # this sub should be started as a helper thread by signalbot
    # programs running under other uids like apache can output to signal->infile
    my $sig = shift;

    # tailing signal infile for stuff to send
    open my $tail, "<", $sig->{infile} or die @_;
    my $inode = (stat($sig->{infile}))[1];

    # SEEK_END
    seek($tail, 0, 2) or die @_;
    for (;;) {
        sleep 1; # not to get too tight loop

        # check if logfiles haven't turned over below our feet
        if ($inode != (stat($sig->{infile}))[1]) {
            close $tail;
            $inode = (stat($sig->{infile}))[1];
            open($tail,$sig->{infile}) or next;
        } else {
            # SEEK_CUR
            seek($tail, 0, 1);
        }
        for (<$tail>) {
            next if $_ =~ /^$/;
            Hermod::relay2sig($_,$sig);
        }
    }
}
